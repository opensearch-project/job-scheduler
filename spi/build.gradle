/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id "org.gradle.test-retry" version "1.6.2"
}

apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.pluginzip'
apply plugin: 'opensearch.testclusters'
apply plugin: 'opensearch.java-agent'
apply plugin: 'opensearch.rest-test'

opensearchplugin {
    name 'opensearch-job-scheduler-spi'
    description 'OpenSearch Job Scheduler SPI Plugin'
    classname 'org.opensearch.jobscheduler.spi.JobSchedulerSpiPlugin'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
}

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE')
}

jacoco {
    toolVersion = '0.8.13'
    reportsDirectory = file("$buildDir/JacocoReport")
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}
check.dependsOn jacocoTestReport

def slf4j_version_of_cronutils = "2.0.17"
dependencies {
    compileOnly "org.opensearch:opensearch:${opensearch_version}"
    // slf4j is the runtime dependency of cron-utils
    // if cron-utils version gets bumped, pls check the slf4j version cron-utils depending on
    //  and bump if needed
    implementation "com.cronutils:cron-utils:9.2.1"
    runtimeOnly "org.slf4j:slf4j-api:${slf4j_version_of_cronutils}"

    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    testImplementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
}

configurations.all {
    if (it.state != Configuration.State.UNRESOLVED) return
    resolutionStrategy {
        force "org.slf4j:slf4j-api:${slf4j_version_of_cronutils}"
    }
}

test {
    retry {
        failOnPassedAfterRetry = false
        maxRetries = 5
    }
    // add "-Dtests.security.manager=false" to VM options if you want to run integ tests in IntelliJ
    systemProperty 'tests.security.manager', 'true'
}

publishing {
    publications {
        // Only keep the pluginZip publication
        pluginZip(MavenPublication) { publication ->
            pom {
                name = "opensearch-job-scheduler-spi"
                description = "OpenSearch Job Scheduler SPI Plugin"
                groupId = "org.opensearch.plugin"
                
                // Add required license information
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                // Add required developer information
                developers {
                    developer {
                        name = 'OpenSearch'
                        url = 'https://github.com/opensearch-project/job-scheduler'
                    }
                }
                
                inceptionYear = '2021'
            }
        }
    }
}

// Clean up conflicting publications
afterEvaluate {
    publishing.publications.removeAll { pub ->
        pub.name in ['nebula', 'shadow']
    }
}
